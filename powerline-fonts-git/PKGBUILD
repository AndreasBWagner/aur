# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Devin Christensen <quixoten at gmail dot com>
#
# Any suggestions welcome; please submit paches via Github:
# https://github.com/alerque/aur/tree/master/git-crypt-git

pkgname=powerline-fonts-git
pkgver=0_51_1ceb8dc
pkgrel=1
pkgdesc="Powerline fonts for X11 and the console"
arch=('any')
url=('https://github.com/Lokaltog/powerline-fonts')
license=('CPL')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('git')
provides=('powerline-fonts')
conflicts=('powerline-fonts')
install=$pkgname.install
source=('git://github.com/Lokaltog/powerline-fonts.git'
        'git://github.com/Lokaltog/powerline.git')
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  #git describe --long --tags | sed 's/^v//;s/-/_/g'
  printf "0_%s_%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir/${pkgname%-git}"

  install -d "$pkgdir/usr/share/fonts/TTF"
  install -d "$pkgdir/usr/share/fonts/OTF"
  install -d "$pkgdir/usr/share/fonts/misc"
  install -d "$pkgdir/usr/share/kbd/consolefonts"
  install -d "$pkgdir/etc/fonts/conf.avail"

  find . -iname "*.ttf" -exec install -m644 {} $pkgdir/usr/share/fonts/TTF \;
  find . -iname "*.otf" -exec install -m644 {} $pkgdir/usr/share/fonts/OTF \;
  find . -iname "*.pcf.gz" -exec install -m644 {} $pkgdir/usr/share/fonts/misc \;
  find . -iname "*.psf.gz" -exec install -m644 {} $pkgdir/usr/share/kbd/consolefonts \;

  cd $srcdir/powerline/font
  install -m644 PowerlineSymbols.otf $pkgdir/usr/share/fonts/OTF/
  install -m644 10-powerline-symbols.conf $pkgdir/etc/fonts/conf.avail/
}
